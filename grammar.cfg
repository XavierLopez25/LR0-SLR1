Program       -> StatementList

StatementList -> Statement StatementList
               | #

Statement     -> IfStmt
               | WhileStmt
               | Assignment

IfStmt        -> IF LPAREN Expression RPAREN COLON Block ElsePart

ElsePart      -> ELSE COLON Block
               | #

WhileStmt     -> WHILE LPAREN Expression RPAREN COLON Block

Assignment    -> Location ASSIGN Expression SEMICOLON

Location      -> ID Indexing

Indexing      -> LBRACKET Expression RBRACKET Indexing
               | #

Block         -> LBRACE StatementList RBRACE

Expression    -> Expression EQUALS AddExpr
               | Expression NOTEQUAL AddExpr
               | Expression LESS AddExpr
               | Expression GREATER AddExpr
               | Expression LESSEQ AddExpr
               | Expression GREATEREQ AddExpr
               | AddExpr

AddExpr       -> AddExpr PLUS MulExpr
               | AddExpr MINUS MulExpr
               | MulExpr

MulExpr       -> MulExpr TIMES UnaryExpr
               | MulExpr DIVIDE UnaryExpr
               | UnaryExpr

UnaryExpr     -> PLUS UnaryExpr
               | MINUS UnaryExpr
               | Primary

Primary       -> ID Indexing
               | NUMBER
               | LPAREN Expression RPAREN